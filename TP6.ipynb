{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "print(\"\"\"\\\n",
    "# *************************************************************************** #\n",
    "# *************************************************************************** #\n",
    "# TP6 : BASES DE GROEBNER ET SYSTEMES POLYNOMIAUX MULTIVARIES                 #\n",
    "# *************************************************************************** #\n",
    "# *************************************************************************** #\n",
    "\"\"\")\n",
    "\n",
    "# CONSIGNES\n",
    "#\n",
    "# Les seules lignes a modifier sont annoncee par \"Code pour l'exercice\"\n",
    "# indique en commmentaire et son signalees\n",
    "# Ne changez pas le nom des variables\n",
    "#\n",
    "# CONSEILS\n",
    "#\n",
    "# Ce modele vous sert a restituer votre travail. Il est deconseille d'ecrire\n",
    "# une longue suite d'instruction et de debugger ensuite. Il vaut mieux tester\n",
    "# le code que vous produisez ligne apres ligne, afficher les resultats et\n",
    "# controler que les objets que vous definissez sont bien ceux que vous attendez.\n",
    "#\n",
    "# Vous devez verifier votre code en le testant, y compris par des exemples que\n",
    "# vous aurez fabrique vous-meme.\n",
    "#\n",
    "\n",
    "\n",
    "reset()\n",
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "#  FONCTIONS DE SAGEMATH\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "MPol.<x,y,z> = PolynomialRing(QQ,3, order='lex')\n",
    "f = 2*x^2*y+7*z^3\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "print(x<y^2)\n",
    "print(f.lt()) #the leading term of the polynomial f\n",
    "print(f.lc()) # the leading coefficient of the polynomial f\n",
    "print(f.lm()) # the leading monomial of the polynomial f\n",
    "\n",
    "reponse  =\"votre reponse ici\"\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print(\"\\n$1/ \", reponse)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "reset()\n",
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# DIVISION MULTIVARIEE\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "MPol.<x,y> = PolynomialRing(QQ,2, order='lex')\n",
    "f  = -x^7 + x^6*y + 2*x^5 - 2*x^4*y - 5*x^2 + 3*x*y^3 + 5*x*y + 11*y^3 + 10 \n",
    "f1 = x*y^2+2*y^2\n",
    "f2 = x^5+5\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "def Div(p,F):\n",
    "    for i in range(len(F)):\n",
    "        if F[i]!=0:\n",
    "            if p.lt()%F[i].lt()==0:\n",
    "                return (True, i)\n",
    "    return (False,-1)\n",
    "\n",
    "def myDivision(f,F):\n",
    "    MPol = f.parent()\n",
    "    n = MPol.ngens()\n",
    "    s = len(F)\n",
    "    Q = [MPol(0)]*s\n",
    "    r = MPol(0)\n",
    "    p = f\n",
    "    while p!=MPol(0):\n",
    "        test, i = Div(p,F)\n",
    "        if test:\n",
    "            Q[i] = Q[i]+p.lt()//F[i].lt()\n",
    "            p = p - ((p.lt()//F[i].lt())*F[i])\n",
    "        else:\n",
    "            r = r + p.lt()\n",
    "            p = p - p.lt()\n",
    "    assert(f==sum(q*g for q,g in zip(Q,F) )+r)\n",
    "    return Q,r\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print(\"\\n$ \",  myDivision(f,[f1,f2]))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "\n",
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# BASE DE GROEBNER\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "MPol.<x,y,z> = PolynomialRing(QQ,3, order='lex')\n",
    "f1 = x^2-y\n",
    "f2 = x*y-z\n",
    "f3 = z^4+x*y\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "def myGroebner(F):\n",
    "    G=F.copy()\n",
    "    S=[1]\n",
    "    s = lambda g, h : (lcm(g.lt(),h.lt())//g.lt())*g - (lcm(g.lt(),h.lt())//h.lt())*h\n",
    "    while S :\n",
    "        S=[]\n",
    "        for g in G:\n",
    "            for h in G:\n",
    "                r = s(g,h)\n",
    "                _, r = myDivision(r,G)\n",
    "                if r!=0:\n",
    "                    S+=[r]\n",
    "        G+=S\n",
    "    G=list(set(G))\n",
    "    return G\n",
    "    \n",
    "def testIfRemoveableElement(G):\n",
    "    for i in range(len(G)):\n",
    "        g = G[i]\n",
    "        if g.lt() in Ideal([f.lt() for f in G if f!=g]):\n",
    "            return (True, i)\n",
    "    return (False,-1)\n",
    "\n",
    "def myRedGroebner(F):\n",
    "    G = myGroebner(F)\n",
    "    b, i = testIfRemoveableElement(G) \n",
    "    while b:\n",
    "        G.pop(i)\n",
    "        b, i= testIfRemoveableElement(G)\n",
    "        while b:\n",
    "            G.pop(i)\n",
    "            b, i = testIfRemoveableElement(G)\n",
    "        for i in range(len(G)):\n",
    "            G[i] = G[i]/G[i].lc()   \n",
    "    return G\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print(\"\\n$1/ \",myGroebner([f1,f2,f3]))\n",
    "print(\"\\n$2/ \",myRedGroebner([f1,f2,f3]))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# APPARTENANCE A UN IDEAL\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "MPol.<x,y,z> = PolynomialRing(QQ,3, order='lex')\n",
    "f1 = x*y-y^2\n",
    "f2 = x^3-z^2\n",
    "I = Ideal([f1,f2])\n",
    "f = -4*x^2*y^2*z^2 + y^6 + 3*z^5\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "test1 = f in I\n",
    "_, r = myDivision(f, I.groebner_basis())\n",
    "test2 = (r==0)  # A ECRIRE VOUS-MEME\n",
    "\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print(\"\\n$ Test de Sage \",test1)\n",
    "print(\"\\n$ Test de personnel \",test2)\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "reset()\n",
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# RESOLUTION D'UN SYSTEME\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "\n",
    "MPol.<x,y> = PolynomialRing(QQ,2,order=\"lex\") # QUEL ORDRE DEVEZ-VOUS CHOISIR ?\n",
    "f = (y^2+6)*(x-1) - y*(x^2 + 1)\n",
    "g = (x^2+6)*(y-1) - x*(y^2 + 1)\n",
    " \n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "I = Ideal(f,g)\n",
    "\n",
    "base = I.groebner_basis() # Vous pouvez utiliser la fonction adhoc de sage\n",
    "          # pour calculer la base Groebner\n",
    "racines_y = [y for (y,_) in base[2].univariate_polynomial().roots()] #\n",
    "racinesf = [(x,racines_y[0]) for (x,_) in f.subs({y:racines_y[0]}).univariate_polynomial().roots()]+ [(x,racines_y[1]) for (x,_) in f.subs({y:racines_y[1]}).univariate_polynomial().roots()]\n",
    "racinesg = [(x,racines_y[0]) for (x,_) in g.subs({y:racines_y[0]}).univariate_polynomial().roots()]+ [(x,racines_y[1]) for (x,_) in g.subs({y:racines_y[1]}).univariate_polynomial().roots()]\n",
    "racinesf = set(racinesf)\n",
    "racinesg = set(racinesg)\n",
    "racines  = [(x,y) for (x,y) in racinesf if (x,y) in racinesg]\n",
    "\n",
    "Gf = implicit_plot(f,(x,0,6),(y,0,6),color='red') \n",
    "Gg = implicit_plot(g,(x,0,6),(y,0,6),color='blue')  \n",
    "Gp = point2d(racines,color='green')\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print(\"\\n$1/  Une base de Groebner de [f,g] est\", base)\n",
    "print(\"\\n$2/  Les valeurs de y sont\", racines_y)\n",
    "print(\"\\n$4/  Les valeurs de (x,y) sont\", racines)\n",
    "print(\"\\n$4/\")\n",
    "#show(Gf+Gg+Gp)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# OPTIMISATION\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "\n",
    "MPol.<x,y,lamb> = PolynomialRing(QQ,3,order='invlex') # QUEL ORDRE DEVEZ-VOUS CHOISIR ?\n",
    "f = x^2*y  - 2*x*y + y + 1\n",
    "g = x^2 + y^2 - 1\n",
    "\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "gradf = vector(MPol,[f.derivative(x),f.derivative(y)])\n",
    "gradg = vector(MPol,[lamb*g.derivative(x),lamb*g.derivative(y)])\n",
    "\n",
    "print(gradf, gradg)\n",
    "\n",
    "syst = [gradg[0]-gradf[0],gradg[1]-gradf[1],g]\n",
    "I = Ideal(syst)\n",
    "base = I.groebner_basis()\n",
    "racines=[]\n",
    "x_possibles = [xx  for (xx,_) in base[2].univariate_polynomial().roots(RR)]\n",
    "y_x_possibles = [(xx,yy) for xx in x_possibles for yy,_ in base[1].subs({x:xx}).univariate_polynomial().roots(RR)]\n",
    "racines=[(xx,yy) for (xx,yy) in y_x_possibles for l,_ in base[0].subs({x:xx,y:yy}).univariate_polynomial().roots(RR)]\n",
    "\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "\n",
    "print(\"\\n$1/  On doit resoudre le systeme\", syst)\n",
    "print(\"\\n$2/  dont une base de Groebner est\", base)\n",
    "print(\"\\n$4/  Les valeurs de (x,y) sont\", racines)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# MANIPULATIONS ALGEBRIQUES\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "MPol.<x,y,u,v> = PolynomialRing(QQ,4,order='degrevlex')\n",
    "# x represents cos(theta), y represents sin(theta)\n",
    "f1=x+y - u\n",
    "f2=x^2 - y^2 + 2*x*y - v\n",
    "f3= x^2 + y^2 - 1\n",
    "I = Ideal(f1,f2,f3)\n",
    "formule = I.reduce(y^6)\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print(\"\\n$1/ \",formule)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# OVALES DE DESCARTES\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "MPol.<x,y,w,z> = PolynomialRing(QQ,4,order='invlex')\n",
    "f1 = w+2*z-3\n",
    "f2 = w^2 - (x^2 + y^2)\n",
    "f3 = z^2 - ((x-1)^2 + y^2)\n",
    "I = Ideal(f1,f2,f3)\n",
    "base = I.groebner_basis()\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "eq = base[-1]\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "MPol2.<x,y> = PolynomialRing(QQ,2,order='lex')\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print(\"\\n$ L'Ã©quation est \",eq)\n",
    "#implicit_plot(MPol2(eq),(x,-2,6),(y,-4,4))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.2",
   "language": "sage",
   "name": "sagemath_9_2_all"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "sage",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
